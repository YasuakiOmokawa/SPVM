package SPVM::ByteList {
  use SPVM::Util (memcpyb, memmoveb);
  
  has values   : byte[];
  has capacity : ro int;
  has length   : ro int;
  
  sub new : SPVM::ByteList () {
    my $capacity = 16;
    my $values = new byte[$capacity];
    
    my $self = new SPVM::ByteList;
    $self->{capacity} = $capacity;
    $self->{length} = 0;
    $self->{values} = $values;
    
    return $self;
  }

  sub new_capacity : SPVM::ByteList ($capacity : int) {
    my $self = new SPVM::ByteList;
    unless ($capacity > 0) {
      die "capacity must be positive";
    }
    $self->{capacity} = $capacity;
    $self->{values} = new byte[$capacity];
    return $self;
  }
  
  sub newa : SPVM::ByteList ($array : byte[]) {
    my $self = new SPVM::ByteList;
    my $length = @$array;
    
    my $values = new byte[$length];
    
    $self->{capacity} = $length;
    $self->{length} = $length;
    
    memcpyb($values, 0, $array, 0, $length);
    
    $self->{values} = $values;
    
    return $self;
  }
  
  private sub _extend : void ($self : self) {
    my $capacity = $self->{capacity};
    my $length = $self->{length};
    my $values = $self->{values};
    
    my $new_capacity = $capacity * 2;
    
    my $new_values = new byte[$new_capacity];
    
    memcpyb($new_values, 0, $values, 0, $length);
    
    $self->{values} = $new_values;
    $self->{capacity} = $new_capacity;
  }
  
  sub insert : void ($self : self, $index : int, $value : byte) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    unless ($index >= 0 && $index <= $length) {
      die "Out of range";
    }
    
    if ($length > $capacity - 1) {
      $self->_extend;
    }
    
    my $values = $self->{values};
    if ($index != $length) {
      my $dist_index = $index + 1;
      my $move_length = $length - $index;
      memmoveb($values, $dist_index, $values, $index, $move_length);
    }
    $values->[$index] = $value;
    
    $self->{length}++;
  }

  sub remove : byte ($self : self, $index : int) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    unless ($index >= 0 && $index < $length) {
      die "Out of range";
    }
    
    my $values = $self->{values};
    my $remove_value = $values->[$index];

    my $dist_index = $index;
    my $src_index = $index + 1;
    my $move_length = $length - $index - 1;
    memmoveb($values, $dist_index, $values, $src_index, $move_length);
    
    $self->{length}--;
    
    return $remove_value;
  }

  sub push : void ($self : self, $value : byte) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    if ($length > $capacity - 1) {
      $self->_extend;
    }
    
    $self->{values}[$length] = $value;
    
    $self->{length}++;
  }
  
  sub pop : byte ($self : self) {
    my $length = $self->length;
    
    if ($length == 0) {
      die "Can't pop";
    }
    
    my $value = $self->{values}[$length - 1];
    
    $self->{length}--;
    
    return $value;
  }
  
  sub unshift : void ($self : self, $value : byte) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    if ($length > $capacity - 1) {
      $self->_extend;
    }
    
    my $values = $self->{values};
    
    memmoveb($values, 1, $values, 0, $length);
    
    $values->[0] = $value;
    $self->{length}++;
  }
  
  sub shift : byte ($self : self) {
    my $length = $self->{length};
    my $capacity = $self->{capacity};
    
    if ($length == 0) {
      die "Can't shift";
    }
    
    my $values = $self->{values};
    
    my $value = $values->[0];
    
    memmoveb($values, 0, $values, 1, $length - 1);
    
    $self->{length}--;
    
    return $value;
  }
  
  sub set : void ($self : self, $index : int, $value : byte) {
    my $length = $self->length;
    
    if ($index < 0 || $index > $length - 1) {
      die "Out of range";
    }
    
    $self->{values}[$index] = $value;
  }
  
  sub get : byte ($self : self, $index : int) {
    my $length = $self->length;
    
    if ($index < 0 || $index > $length - 1) {
      die "Out of range";
    }
    
    my $value = $self->{values}[$index];
    
    return $value;
  }
  
  sub to_array : byte[] ($self : self) {
    my $length = $self->length;
    
    my $array = new byte[$length];
    
    my $values = $self->{values};
    
    memcpyb($array, 0, $values, 0, $length);
    
    return $array;
  }
}
